{
    --------------------------------------------
    Filename: CC2500-TXDemo.spin
    Author: Jesse Burt
    Description: Simple transmit demo of the cc2500 driver
    Copyright (c) 2021
    Started Nov 23, 2019
    Updated Jan 10, 2021
    See end of file for terms of use.
    --------------------------------------------
}
CON

    _clkfreq        = cfg._clkfreq_def
    _xtlfreq        = cfg._xtlfreq

' -- User-modifiable constants
    LED             = cfg.LED1
    SER_BAUD        = 2_000_000

    CS_PIN          = 0                             ' Change to your module's connections
    SCK_PIN         = 1
    MOSI_PIN        = 2
    MISO_PIN        = 3

    NODE_ADDRESS    = $02
' --

OBJ

    ser         : "com.serial.terminal.ansi"
    cfg         : "core.con.boardcfg.p2eval"
    time        : "time"
    int         : "string.integer"
    cc2500      : "wireless.transceiver.cc2500.spi"

VAR

    long _fifo[16]
    byte _pktlen

PUB Main()

    setup()

    cc2500.gpio0(cc2500.IO_HI_Z)                ' set GPIO0 to hi-Z mode
    cc2500.autocal(cc2500.IDLE_RXTX)            ' calibrate on idle to RX/TX
    cc2500.idle()

    ser.str(string("Waiting for radio idle status..."))
    repeat until cc2500.state() == 1
    ser.strln(string("done"))

    cc2500.carrierfreq(2_401_000)               ' set carrier frequency

    ser.str(string("Waiting for PLL lock..."))
    repeat until cc2500.plllocked() == TRUE     ' wait until PLL is locked
    ser.strln(string("done"))

    cc2500.txpowerindex(0)

    ' transmit power levels:
    ' -55, -30, -28, -26, -24, -22, -20, -18, -16, -14, -12, -10, -8, -6, -4,
    ' -2, 0, 1
    cc2500.txpower(0)
    ser.str(string("TXPower: "))
    ser.dec(cc2500.txpower(-255))               ' confirm current value
    ser.strln(string("dBm"))

    ser.strln(string("Press any key to begin transmitting"))
    ser.charin()

    transmit()

PUB Transmit() | count, tmp, to_node

    _pktlen := 10
    cc2500.nodeaddress(NODE_ADDRESS)            ' this node's address
    cc2500.payloadlencfg(cc2500.PKTLEN_FIXED)   ' fixed payload length mode
    cc2500.payloadlen(_pktlen)                  ' set payload length
    cc2500.crccheckenabled(TRUE)                ' enable CRC checks
    cc2500.syncmode(cc2500.SYNCMODE_3032_CS)    ' accept payload as valid if:
    cc2500.appendstatus(FALSE)                  ' 30 of 32 syncword bits match
                                                ' Carrier sense > threshold
    to_node := $01

    ser.clear()
    ser.position(0, 0)
    ser.str(string("Transmit mode - "))
    ser.dec(cc2500.carrierfreq(-2))
    ser.strln(string("Hz"))
    ser.str(string("Transmitting to node $"))
    ser.hex(to_node, 2)

    _fifo.byte[0] := to_node                    ' address of node to send to
    _fifo.byte[1] := NODE_ADDRESS               ' this node's address
    _fifo.byte[2] := "T"                        ' start of payload
    _fifo.byte[3] := "E"
    _fifo.byte[4] := "S"
    _fifo.byte[5] := "T"

    count := 0
    cc2500.aftertx(cc2500.TXOFF_IDLE)           ' change to state after tx
    repeat
        tmp := int.deczeroed(count++, 4)        ' tack a counter onto the
        bytemove(@_fifo.byte[6], tmp, 4)        '   end of the payload
        ser.position(0, 10)
        ser.str(string("Sending "))
        ser.str(@_fifo)
        cc2500.idle()
        cc2500.flushtx()
        cc2500.fstx()
        cc2500.txmode()
        cc2500.txpayload(_pktlen, @_fifo)

        ' wait between packets to reduce abuse of the airwaves
        time.sleep(5)

PUB Setup()

    ser.start(SER_BAUD)
    waitms(100)
    ser.clear()
    ser.strln(string("Serial terminal started"))
    if cc2500.start(CS_PIN, SCK_PIN, MOSI_PIN, MISO_PIN)
        ser.strln(string("CC2500 driver started"))
    else
        ser.strln(string("CC2500 driver failed to start - halting"))
        repeat

DAT
' Radio states
MARC_STATE  byte    "SLEEP           ", 0 {0}
            byte    "IDLE            ", 0 {1}
            byte    "XOFF            ", 0 {2}
            byte    "VCOON_MC        ", 0 {3}
            byte    "REGON_MC        ", 0 {4}
            byte    "MANCAL          ", 0 {5}
            byte    "VCOON           ", 0 {6}
            byte    "REGON           ", 0 {7}
            byte    "STARTCAL        ", 0 {8}
            byte    "BWBOOST         ", 0 {9}
            byte    "FS_LOCK         ", 0 {10}
            byte    "IFADCON         ", 0 {11}
            byte    "ENDCAL          ", 0 {12}
            byte    "RX              ", 0 {13}
            byte    "RX_END          ", 0 {14}
            byte    "RX_RST          ", 0 {15}
            byte    "TXRX_SWITCH     ", 0 {16}
            byte    "RXFIFO_OVERFLOW ", 0 {17}
            byte    "FSTXON          ", 0 {18}
            byte    "TX              ", 0 {19}
            byte    "TX_END          ", 0 {20}
            byte    "RXRX_SWITCH     ", 0 {21}

DAT
{
    --------------------------------------------------------------------------------------------------------
    TERMS OF USE: MIT License

    Permission is hereby granted, free of charge, to any person obtaining a copy of this software and
    associated documentation files (the "Software"), to deal in the Software without restriction, including
    without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
    copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the
    following conditions:

    The above copyright notice and this permission notice shall be included in all copies or substantial
    portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT
    LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
    IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
    WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
    SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
    --------------------------------------------------------------------------------------------------------
}

